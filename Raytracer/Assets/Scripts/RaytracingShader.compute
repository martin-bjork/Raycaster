#pragma kernel TraceRays

static const float PI = 3.14159265f;
static const float REFLECTION_OFFSET = 0.001f;

RWTexture2D<float4> Result;
float4x4 CameraToWorld;
float4x4 CameraInverseProjection;
Texture2D<float4> SkyboxTexture;
SamplerState sampler_SkyboxTexture;
float2 PixelOffset;

struct Ray {
    float3 origin;
    float3 direction;
    float3 energy;
};

struct RayHit {
    float3 position;
    float distance;
    float3 normal;
};

Ray CreateRay(float3 origin, float3 direction) {
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    ray.energy = float3(1, 1, 1);
    return ray;
}

RayHit CreateRayHit() {
    RayHit hit;
    hit.position = float3(0, 0, 0);
    hit.distance = 1.#INF;
    hit.normal = float3(0, 0, 0);
    return hit;
}

Ray CreateCameraRay(float2 uv) {
    float3 origin = mul(CameraToWorld, float4(0, 0, 0, 1)).xyz;
    float3 direction = mul(CameraInverseProjection, float4(uv, 0, 1)).xyz;
    direction = mul(CameraToWorld, float4(direction, 0)).xyz;
    direction = normalize(direction);
    return CreateRay(origin, direction);
}

float2 CalculateUVFromGroupIndex(float2 id) {
    uint width;
    uint height;
    Result.GetDimensions(width, height);
    return float2((id + PixelOffset) / float2(width, height) * 2 - 1);
}

float3 SampleSkybox(float3 direction) {
    float theta = acos(direction.y) / -PI;
    float phi = atan2(direction.x, -direction.z) / -PI * 0.5f;
    return SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz;
}

void IntersectGroundPlane(Ray ray, inout RayHit bestHit) {
    float t = -ray.origin.y / ray.direction.y;
    if (t > 0 && t < bestHit.distance) {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = float3(0, 1, 0);
    }
}

void IntersectSphere(Ray ray, inout RayHit bestHit, float4 sphere) {
    float3 d = ray.origin  - sphere.xyz;
    float p1 = -dot(ray.direction, d);
    float p2sqr = p1 * p1 - dot(d, d) + sphere.w * sphere.w;
    if (p2sqr < 0) {
        return;
    }
    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < bestHit.distance) {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = normalize(bestHit.position - sphere.xyz);
    }
}

RayHit Trace(Ray ray) {
    RayHit bestHit = CreateRayHit();
    IntersectGroundPlane(ray, bestHit);
    //IntersectSphere(ray, bestHit, float4(0, 3, 0, 1));
    for (int i = -20; i < 20; i++) {
        for (int j = -20; j < 20; j++) {
            IntersectSphere(ray, bestHit, float4(i * 3, 1, j * 3, 1));
        }
    }
    return bestHit;
}

float3 Shade(inout Ray ray, RayHit hit) {
    if (hit.distance < 1.#INF) {
        float3 specular = float3(0.6f, 0.6f, 0.6f);
        ray.origin = hit.position + hit.normal * REFLECTION_OFFSET;
        ray.direction = reflect(ray.direction, hit.normal);
        ray.energy *= specular;
        return float3(0, 0, 0);
    } else {
        ray.energy = 0;
        return SampleSkybox(ray.direction);
    }
}

float3 TraceAndShade(Ray ray) {
    float3 result = float3(0, 0, 0);
    for (int i = 0; i < 8; i++) {
        RayHit hit = Trace(ray);
        result += ray.energy * Shade(ray, hit);
        if (!any(ray.energy)) {
            break;
        }
    }
    return result;
}

[numthreads(8,8,1)]
void TraceRays (uint3 id : SV_DispatchThreadID) {

    float2 uv = CalculateUVFromGroupIndex(id.xy);

    Ray ray = CreateCameraRay(uv);

    float4 result = float4(TraceAndShade(ray), 1);

    Result[id.xy] = result;
}
